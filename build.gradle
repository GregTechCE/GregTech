buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.1.0'
}

apply plugin: 'net.minecraftforge.gradle.forge'

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

def mod_version = getVersionFromJava(file("src/main/java/gregtech/GregTechVersion.java"))
version = "${config.minecraft.version}-${mod_version}"
group = "gregtech"
archivesBaseName = "gregtech"

minecraft {
    version = "${config.minecraft.version}-${config.forge.version}"
	mappings = "snapshot_20170928"
    runDir = "run"
    useDepAts = true
}

configurations {
    provided
    embedded
    compile.extendsFrom provided, embedded
}

repositories {
    maven {
        name = "ic2, forestry"
        url = "http://maven.ic2.player.to/"
    }
    maven { //JEI
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "tterrag maven"
        url = "http://maven.tterrag.com/"
    }
    maven {
        name = "ChickenBones maven"
        url = "http://chickenbones.net/maven/"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven {
        name = "tehnut maven"
        url = "http://tehnut.info/maven/"
    }
    maven {
        name = "CraftTweaker Maven"
        url = "https://maven.blamejared.com/"
    }
}

String mcVersion = config.minecraft.version
String shortVersion = mcVersion.substring(0, mcVersion.lastIndexOf('.'))
String strippedVersion = shortVersion.replace(".", "") + "0"

dependencies {
    deobfCompile("net.sengir.forestry:forestry_${config.minecraft.version}:${config.forestry.version}") {
        transitive = false
    }

    deobfCompile "codechicken:ChickenASM:${shortVersion}-${config.chickenasm.version}"
    deobfCompile "codechicken:CodeChickenLib:${config.minecraft.version}-${config.ccl.version}:deobf"
    deobfCompile "codechicken:ForgeMultipart:${config.minecraft.version}-${config.multipart.version}:deobf"
    deobfCompile "CraftTweaker2:CraftTweaker2-MC${strippedVersion}-Main:${config.crafttweaker.version}"
    deobfCompile "mezz.jei:jei_${config.minecraft.version}:${config.jei.version}:api"
    deobfCompile "mcjty.theoneprobe:TheOneProbe-${shortVersion}:${shortVersion}-${config.top.version}"
    deobfCompile "cofh:ThermalDynamics:${config.minecraft.version}-${config.thermaldynamics.version}:deobf"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

compileJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", mod_version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':mod_version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
	
	// access transformer
	rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
	manifest {
	    attributes 'FMLAT': 'gregtech_at.cfg'
	}
}

task source(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task dev(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
    archives jar
	archives source
}

idea {
    module {
        inheritOutputDirs = true
    }
}

task ciWriteBuildNumber doLast {
    def file = file("src/main/java/gregtech/GregTechVersion.java")
    def bn = System.getenv("BUILD_NUMBER") ?: 0
    def outfile = ""
    def ln = "\n" //Linux line endings because we're on git!

    println "Build number: $bn"

    file.eachLine { String s ->
        if (s.matches("^ {4}public static final int BUILD = [\\d]+;\$"))
            s = "    public static final int BUILD = ${bn};"
        outfile += (s + ln)
    }
    file.write(outfile)
}

String getVersionFromJava(File file) {
    String major = "0"
    String minor = "0"
    String revision = "0"

    String prefix = "public static final int"
    file.eachLine { String s ->
        s = s.trim()
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 1)
            s = s.replace('=', ' ').replaceAll(" +", " ").trim()
            String[] pts = s.split(" ")

            if (pts[0] == "MAJOR") major = pts[pts.length - 1]
            else if (pts[0] == "MINOR") minor = pts[pts.length - 1]
            else if (pts[0] == "REVISION") revision = pts[pts.length - 1]
        }
    }

    def branchName = System.getenv("CI_COMMIT_REF_NAME")
    if (branchName != null && branchName != "master") {
        return "$major.$minor.$revision-$branchName"
    }

    String build = System.getenv("BUILD_NUMBER") ?: 0

    return "$major.$minor.$revision.$build"
}

curseforge {
    if (!System.getenv('CURSE_API_KEY')) {
        return
    }

    apiKey = System.getenv('CURSE_API_KEY')
    project {
        id = '293327'
        changelog = file('CHANGELOG.md')
        changelogType = 'markdown'
        releaseType = 'beta'

        relations {
            requiredDependency 'codechicken-lib-1-8'
            optionalDependency 'forge-multipart-cbe'
            optionalDependency 'crafttweaker'
            optionalDependency 'jei'
            optionalDependency 'the-one-probe'
        }

        addArtifact source
    }
}
